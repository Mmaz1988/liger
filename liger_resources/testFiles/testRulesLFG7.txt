-replace(true);

#a TNS-ASP #b ==> #a SEM #c & #c TEMP-REF #d & #d T-REF 'undefined'.

#a TNS-ASP #b TENSE 'past' & #a SEM #c TEMP-REF #d & #d T-REF 'undefined' ==>  #d T-REF 'past' & #d EVAL #e & #e TIME 'now'.
#a TNS-ASP #b TENSE 'pres' & #a SEM #c TEMP-REF #d & #d T-REF 'undefined' ==>  #d T-REF 'pres' & #d EVAL #e & #e TIME 'now'.
#a TNS-ASP #b TENSE 'fut' & #a SEM #c TEMP-REF #d & #d T-REF 'undefined' ==>  #d T-REF 'fut' & #d EVAL #e & #e TIME 'now'.

//#a TNS-ASP #b TENSE 'pres' & #a SEM #c ==> #c TEMP-REF #d & #d T-REF 'pres' & #d EVAL #e & #e TIME 'now'.
//#a TNS-ASP #b TENSE 'fut' & #a SEM #c ==> #c TEMP-REF #d & #d T-REF 'fut' & #d EVAL #e & #e TIME 'now'.

#a TNS-ASP #b PROG '+_' & #a SEM #c ==> #c VIEWPOINT #d & #d ASPECT 'impv' & #d A-RESTR 'ongoing'.
#a TNS-ASP #b PROG '-_' & #a SEM #c ==> #c VIEWPOINT #d & #d ASPECT 'prv' & #d A-RESTR 'bounded'.
#a TNS-ASP #b PERF '+_' & #a SEM #c ==> #c ASP-TENSE #d & #d A-REF 'past'.

//undefined FPS for transitive verbs
#a TNS-ASP #b & #a PRED %a & #a SEM #z  ==>
#z FPS #y & #y CHECK #x.

#a TNS-ASP #b & #a PRED %a & #a SEM #z & #a SUBJ #c & #a OBJ #d & strip(%a) == 'kiss' &
#z FPS #e & #e CHECK #x ==>
#e CHECK '-' &
#e initP #f &
#f INIT #a &
#f I-SUBJ #c &
#e procP #g &
#g PROC #a &
#g P-SUBJ #d &
#e XP #h &
#h REF 'unspec' &
#h TYPE 'rheme'.

#a TNS-ASP #b & #a PRED %a & #a SEM #z & #a SUBJ #c & #a OBJ #d & strip(%a) == 'visit' &
#z FPS #e & #e CHECK #x ==>
#e CHECK '-' &
#e initP #f &
#f INIT #a &
#f I-SUBJ #c &
#e procP #g &
#g PROC #a &
#g P-SUBJ #c &
#e XP #h &
#h REF 'unspec' &
#h TYPE 'rheme'.

#a TNS-ASP #b & #a PRED %a & #a SEM #z & #a SUBJ #c & #a OBJ #d & #a OBJ-TH #e & strip(%a) == 'give' &
#f FPS #g & #g CHECK #h ==>
#g CHECK '-' &
#g initP #i &
#i INIT #a &
#i I-SUBJ #c &
#g procP #j &
#j PROC #a &
#j P-SUBJ 'undefined' &
#g resP #k &
#k RES #a &
#k R-SUBJ #d &
#g XP #l &
#l REF #e &
#l TYPE 'possession'.

//Semantic interpretation

#a SUBJ #g NTYPE #h & #g PRED %g
==> #g SEM #i & #i GLUE strip(%g) : #i.

#a OBJ #g NTYPE #h & #g PRED %g
==> #g SEM #i & #i GLUE strip(%g) : #i.

#a OBJ-TH #g NTYPE #h & #g PRED %g
==> #g SEM #i & #i GLUE strip(%g) : #i.

//NP Quantifier -- Sem structure
#g ^(SPEC) #h & #g QUANT #i & #h SEM #l ==> #l VAR #j & #l RESTR #k & #l SIT #s.
#g ^(SPEC) #h & #g DET #i & #h SEM #l ==> #l VAR #j & #l RESTR #k & #l SIT #s.

#a PRED %a & #a PRON-REL #b & #a SUBJ #b &
#a SEM #z & #b SEM #y &
#a ^(inSet>ADJUNCT) #c SEM #d VAR #e & #d RESTR #f 6 #d SIT #s
==> #y GLUE [/P_<e,t>.[/Q_<e,t>.[/x_e.(P(x) \& Q(x))]]] : ((#e -o #f) -o ((#y -o #z) -o (#e -o #f))).

//NP Quantifier instantiation
#g ^(SPEC) #h SEM #i VAR #j & #i RESTR #k & #i SIT #s &
#g QUANT #l PRED %l & %l == 'every' &
#h ^(%) #m SEM #n FPS #b & #n VIEWPOINT #o
==> #i GLUE [/P_<e,<s,t>>.[/Q_<e,<s,t>>.[/s_s.Ax_e[P(x)(s) -> Q(x)(s)]]]] : ((#j -o (#s -o #k)) -o ((#i -o (#o -o #n)) -o (#o -o #n))).

#g ^(SPEC) #h SEM #i VAR #j & #i RESTR #k & #i SIT #s &
#g DET #l PRED %l &
#h ^(%) #m SEM #n FPS #b & #n VIEWPOINT #o
==> #i SIT #s & #i GLUE [/P_<e,<s,t>>.[/Q_<e,<s,t>>.[/s_s.Ex_e[P(x)(s) \& Q(x)(s)]]]] : ((#j -o (#s -o #k)) -o ((#i -o (#o -o #n)) -o (#o -o #n))).

//SUBJ predicates for Quantifiers
#g SEM #j VAR #i & #j RESTR #k & #j SIT #a & #g PRED %g ==>#g GLUE [/x_e.[/s_s.strip(%g)(x,s)] : (#i -o (#a -o #k)).


//FPS rules

#a SEM #z FPS #y CHECK #x & #a PRED %a ==> #x GLUE [/e_v.strip(%a)(e)] : (#x -o #y).

#a XP #b REF 'unspec' & #b TYPE 'rheme' ==> #b GLUE y : #b.


//RES
#a XP #b &
#b REF #c &
#b TYPE 'possession' &
#a resP #d &
#d RES #e &
#e PRED %e &
#d R-SUBJ #f ==>
#b GLUE [/x_e.[/y_e.[/e_v.(have(e) \& (ag(e,x) \& th(e,y)))]]] : (#f -o (#c -o (#e -o #b))) &
#d GLUE [/P_<e,<v,t>>.[/x_e.[/e_v.(strip(%e)(e) \& (res(e,x) \& P(x)(e)))]]] : ((#f -o (#e -o #b)) -o (#f -o (#e -o #d))).

//PROC
#d SEM #z FPS #b procP #c PROC #d & #c P-SUBJ 'undefined' &
#b resP #g RES #d &
#b XP #f TYPE 'possession' & #f REF #j &
#d PRED %a ==>
#c GLUE [/P_<v,t>.[/e_v.Ee1_v[Ee2_v[strip(%a)(e1) \& (equals(e,to(e1,e2)) \& (proc(e1) \& P(e2)))]]]] : ((#d -o #g) -o (#d -o #c)).

#a SEM #z FPS #b procP #c PROC #d & #c P-SUBJ #e & #b XP #f TYPE 'rheme' & #a PRED %a ==>
#c GLUE [/y_s.[/x_e.[/e_v.(strip(%a)(e) \& proc(e,x,y))]]] : (#f -o (#e -o (#d -o #c))).



//INIT
#a SEM #z FPS #b initP #c I-SUBJ #d & #c INIT #h & #b procP #e PROC #f & #e P-SUBJ #g & #a PRED %a ==>
#c GLUE [/P_<v,t>.[/x_e.[/e_v.Ee1_v[Ee2_v[strip(%a)(e1) \& (equals(e,to(e1,e2)) \& (init(e1,x) \& P(e2)))]]]]] : ((#f -o #e) -o (#d -o (#h -o #c))).

#a SEM #z FPS #b initP #c I-SUBJ #d & #c INIT #h & #b procP #e PROC #f & #e P-SUBJ 'undefined' & #a PRED %a ==>
#c GLUE [/P_<v,t>.[/x_e.[/e_v.Ee1_v[Ee2_v[strip(%a)(e1) \& (equals(e,to(e1,e2)) \& (init(e1,x) \& P(e2)))]]]]] : ((#f -o #e) -o (#d -o (#h -o #c))).

#a SEM #z FPS #b initP #c I-SUBJ #d & #c INIT #h & #b procP #e PROC #f & #e P-SUBJ #d & #a PRED %a ==>
#c GLUE [/P_<e,<v,t>>.[/x_e.[/e_v.Ee1_v[Ee2_v[strip(%a)(e1) \& (equals(e,to(e1,e2)) \& (init(e1,x) \& P(x)(e2)))]]]]] : ((#d -o (#f -o #e)) -o (#d -o (#h -o #c))).


//Subcategorization for verbs
#a SUBJ #b & #a OBJ #c & #a TNS-ASP #d & #a PRED %a &
#a SEM #z FPS #e procP #f P-SUBJ #c SEM #j &
#e initP #h INIT #i & #h I-SUBJ #b SEM #k ==>
#e GLUE [/R_<e,<e,<v,t>>>.[/x_e.[/y_e.[/e_v.(strip(%a)(e) \& (R(x)(y)(e) \& (ag(e,x) \& pt(e,y))))]]]] : ((#b -o (#c -o (#i -o #h))) -o (#k -o (#j -o (#h -o #e))).

#a SUBJ #b & #a OBJ #c & #a OBJ-TH #d & #a PRED %a &
#d SEM #h &
#a SEM #z FPS #e resP #f R-SUBJ #c SEM #g &
#e initP #k INIT #l & #k I-SUBJ #b SEM #m ==>
#e GLUE [/R_<e,<e,<e,<v,t>>>>.[/x_e.[/y_e.[/z_e.[/e_v.(strip(%a)(e) \& (R(x)(y)(z)(e) \& (ag(e,x) \& (theme(e,z) \& goal(e,y)))))]]]]] : ((#b -o (#c -o (#d -o (#l -o #k)))) -o (#m -o (#g -o (#h -o (#k -o #e)))).

#a SUBJ #b SEM #c &
#a SEM #z FPS #f & #f CHECK #y ==>
#f GLUE [/R_<v,t>.[/x_e.[/y_e.[/e_v.(R(e) \& ag(e,x))]]]] : ((#y -o #f) -o (#c -o (#y -o #f))).

#a SUBJ #b SEM #c & #a OBJ #d SEM #e &
#a SEM #z FPS #f & #f CHECK #y ==>
#f GLUE [/R_<v,t>.[/x_e.[/y_e.[/e_v.(R(e) \& (ag(e,x) \& pt(e,y)))]]]] : ((#y -o #f) -o (#c -o (#e -o (#y -o #f))).

#a SUBJ #b & #a OBJ #c & #a TNS-ASP #d & #a PRED %a &
#a SEM #z FPS #e procP #f P-SUBJ #b SEM #j &
#e initP #h INIT #i & #h I-SUBJ #b SEM #j &
#c SEM #k
 ==>
#e GLUE [/R_<e,<v,t>>.[/x_e.[/y_e.[/e_v.(strip(%a)(e) \& (R(x)(e) \& (ag(e,x) \& pt(e,y))))]]]] : ((#b -o (#i -o #h)) -o (#j -o (#k -o (#h -o #e))))).


//Verb template for comp verbs
#g SEM #j EV #n &
#g SUBJ #h SEM #i &
#g COMP #l TNS-ASP #o SEM #m TENSE #e &
#l SEM #s &
#g TNS-ASP #a SEM #b ASPECT #c RESTR #d ==> #g GLUE [/R_<v,<s,t>>.[/P_<s,t>.[/x_e.[/s_s.Ee_v[R(e)(s) \& (agent(e,x) \& theme(e,P(s)))]]]]] : ((#n -o (#d -o #j)) -o ((#e -o #s) -o (#i -o (#c -o #j)))).


//Closure
#b FPS #c ==> #c CLOSURE #d.
#b FPS #c initP #d & #c CLOSURE #e ==> #e GLUE [/P_<v,t>.Ee_v[P(e)]] : ((#d -o #c) -o #c).
#b FPS #c CLOSURE #e & #c CHECK #z ==> #e GLUE [/P_<v,t>.Ee_v[P(e)]] : ((#z -o #c) -o #c).


#a TNS-ASP #b & #a SEM #z FPS #c & #z VIEWPOINT #y
==> #d GLUE [/P_t.[/s_s.exemplify(s,P)]] : (#c -o (#y -o #z)).


#a SEM #b VIEWPOINT #c ==>
#c VAR #d & #c RESTR #e &
#c ASP-RESTR #f.


#a SEM #b VIEWPOINT #c A-RESTR 'ongoing' &
#c VAR #d & #c RESTR #e &
#c ASP-RESTR #f ==>
#f GLUE [/s_s.[/t_s.ongoing(t,s)]] : (#d -o (#e -o #c)).

#a SEM #b VIEWPOINT #c A-RESTR 'bounded' &
#c VAR #d & #c RESTR #e &
#c ASP-RESTR #f ==>
#f GLUE [/s_s.[/t_s.bounded(t,s)]] : (#d -o (#e -o #c)).


#a SEM #b VIEWPOINT #c ASPECT 'impv' &
#c VAR #d & #c RESTR #e &
#b TEMP-REF #f
 ==>  #c GLUE [/M_<s,<s,t>>.[/P_<s,t>.[/s_s.Az_s[M(s)(z) -> P(z)]]]] : ((#d -o (#e -o #c)) -o ((#c -o #b) -o (#f -o #b))).

#a SEM #b VIEWPOINT #c ASPECT 'prv' &
#c VAR #d & #c RESTR #e &
#b TEMP-REF #f
 ==>  #c GLUE [/M_<s,<s,t>>.[/P_<s,t>.[/s_s.Ez_s[M(s)(z) \& P(z)]]]] : ((#d -o (#e -o #c)) -o ((#c -o #b) -o (#f -o #b))).


#a SEM #b TEMP-REF #c T-REF 'past' & #c EVAL #d ==> #c GLUE [/P_<s,t>.[/s_s.Er_s[before(r,s) \& P(r)]]] : ((#c -o #b) -o (#d -o #b)).

#a SEM #b TEMP-REF #c T-REF 'pres' & #c EVAL #d ==> #c GLUE [/P_<s,t>.[/s_s.Er_s[equals(r,s) \& P(r)]]] : ((#c -o #b) -o (#d -o #b)).

#a SEM #b ASP-TENSE #c A-REF 'past' & #b TEMP-REF #d ==>  #c GLUE [/P_<s,t>.[/s_s.Er_s[before(r,s) \& P(r)]]] : ((#d -o #b) -o (#d -o #b)).

#a SEM #b ASP-TENSE #c A-REF 'undefined' & #b TEMP-REF #d ==>  #c GLUE [/P_<s,t>.[/s_s.P(s)]] : ((#d -o #b) -o (#d -o #b)).

#a EVAL #b & #b TIME %i & %i == 'now' ==> #b GLUE now : #b.

